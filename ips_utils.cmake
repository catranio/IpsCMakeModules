function(_ips_add_prefix_to_list _PREFIX _LIST_VAR _LIST)
    foreach(_LIST_ITEM ${_LIST})
        list(APPEND _VAL "${_PREFIX}${_LIST_ITEM}")
    endforeach()
    set(${_LIST_VAR} ${_VAL} PARENT_SCOPE)
endfunction(_ips_add_prefix_to_list)

function(_ips_collect_source _IPS_COLLECTED_SOURCES _IPS_SOURCES _IPS_EXTENSIONS)
    if (NOT _IPS_EXTENSIONS)
        set(_IPS_EXTENSIONS *.cpp *.cxx *.cc *.c *.h *.hpp *.hh *.hxx)
    endif()

    foreach(_SOURCE ${_IPS_SOURCES})
        get_filename_component(_ABSOLUTE_SOURCE ${_SOURCE} ABSOLUTE)
        if(EXISTS ${_ABSOLUTE_SOURCE})
            if (IS_DIRECTORY ${_ABSOLUTE_SOURCE})
                _ips_add_prefix_to_list("${_ABSOLUTE_SOURCE}/" _SOURCE_WITH_EXT ${_IPS_EXTENSIONS})
                file(GLOB_RECURSE _SRC_DIR_CONTENTS ${_SOURCE_WITH_EXT})
                list(APPEND _SOURCE_LIST ${_SRC_DIR_CONTENTS})
            else()
                list(APPEND _SOURCE_LIST ${_ABSOLUTE_SOURCE})
            endif()
        else()
            file(GLOB_RECURSE _GLOB_CONTENTS ${_ABSOLUTE_SOURCE})
            if (_GLOB_CONTENTS)
                list(APPEND _SOURCE_LIST ${_GLOB_CONTENTS})
            endif()
        endif()
    endforeach()

    if(_SOURCE_LIST)
        list(REMOVE_DUPLICATES _SOURCE_LIST)
    endif()

    set(${_IPS_COLLECTED_SOURCES} ${_SOURCE_LIST} PARENT_SCOPE)
endfunction(_ips_collect_source)

function (ips_add_sanitizer target SANITIZER)
    cmake_parse_arguments(
            SANITIZER
            ""
            "CONDITION"
            ""
            ${ARGN}
    )
    if (SANITIZER_CONDITION)
        message(STATUS "${target}: Using sanitizer: ${SANITIZER}")
        target_compile_options(${target} PRIVATE -fsanitize=${SANITIZER})
        target_link_options(${target} PRIVATE -fsanitize=${SANITIZER})
    endif ()
endfunction ()

function(_ips_split VALUE DELIMITER)
    list(LENGTH ARGN ARGN_COUNT)
    math(EXPR ARGN_COUNT "${ARGN_COUNT}-1")
    string(LENGTH "${DELIMITER}" DELIMITER_LEN)
    foreach(I RANGE ${ARGN_COUNT})
        string(FIND ${VALUE} ${DELIMITER} POS)
        list(GET ARGN ${I} OUTPUT_VARIABLE)
        if(NOT POS EQUAL -1)
            math(EXPR NEXT "${POS}+${DELIMITER_LEN}")
            string(SUBSTRING ${VALUE} 0 ${POS} PIECE)
            string(SUBSTRING ${VALUE} ${NEXT} -1 VALUE)
            set(${OUTPUT_VARIABLE} ${PIECE} PARENT_SCOPE)
        else()
            set(${OUTPUT_VARIABLE} ${VALUE} PARENT_SCOPE)
            if(${I} LESS ${ARGN_COUNT})
                math(EXPR I_PLUS_ONE "${I}+1")
                foreach(J RANGE ${I_PLUS_ONE} ${ARGN_COUNT})
                    list(GET ARGN ${J} OUTPUT_VARIABLE)
                    unset(${OUTPUT_VARIABLE} PARENT_SCOPE)
                endforeach()
            endif()
            break()
        endif()
    endforeach()
endfunction()

function(ips_split_name_and_namespace _OUTPUT_NAMESPACE _OUTPUT_NAME _NAME)
    _ips_split(${_NAME} "::" _RETVAL_NAMESPACE _RETVAL_NAME)
    if(NOT DEFINED _RETVAL_NAME)
        set(_RETVAL_NAME ${_RETVAL_NAMESPACE})
        set(_RETVAL_NAMESPACE "")
    endif()
    set(${_OUTPUT_NAMESPACE} ${_RETVAL_NAMESPACE} PARENT_SCOPE)
    set(${_OUTPUT_NAME} ${_RETVAL_NAME} PARENT_SCOPE)
endfunction()

