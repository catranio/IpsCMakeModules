function(ips_fetch deps)
    cmake_parse_arguments(IPS "" "GIT_REPOSITORY;GIT_TAG;SOURCE_DIR;LOCAL_DIR" "" ${ARGN})
    ips_split_name_and_namespace(deps_namespace deps_name ${deps})

    if (IPS_LOCAL_DIR)
        if (EXISTS IPS_LOCAL_DIR)
            set(IPS_GIT_REPOSITORY ${IPS_LOCAL_DIR})
            set(opts "${opts} | local path: ${IPS_LOCAL_DIR}")
        else ()
            message(FATAL_ERROR "For target ${deps_name}: path '${IPS_LOCAL_DIR}' does not exist.")
        endif ()
    endif()

    if (IPS_GIT_TAG)
        set(opts "${opts} | tag: ${IPS_GIT_TAG}")
        set(fetch_cmd ${fetch_cmd} GIT_TAG ${IPS_GIT_TAG})
    elseif (IPS_PREFER_GIT_TAG)
        execute_process(COMMAND ${GIT_EXECUTABLE} ls-remote ${IPS_GIT_REPOSITORY} rev-list ${IPS_PREFER_GIT_TAG}
                OUTPUT_VARIABLE is_exist)
        if (is_exist)
            set(opts "${opts} | tag(prefer): ${IPS_PREFER_GIT_TAG}")
            set(fetch_cmd ${fetch_cmd} GIT_TAG ${IPS_PREFER_GIT_TAG})
        endif ()
    endif ()
    if (IPS_SOURCE_DIR)
        set(fetch_cmd ${fetch_cmd} SOURCE_DIR ${IPS_SOURCE_DIR})
        set(opts "${opts} | source path: ${IPS_SOURCE_DIR}")
    endif ()
    if (IPS_UNPARSED_ARGUMENTS)
        set(fetch_cmd ${fetch_cmd} ${IPS_UNPARSED_ARGUMENTS})
        set(opts "${opts} | args: ${IPS_UNPARSED_ARGUMENTS}")
    endif ()
    set(fetch_cmd GIT_REPOSITORY ${IPS_GIT_REPOSITORY} GIT_SHALLOW TRUE ${fetch_cmd})

    include(FetchContent)
    ips_split_name_and_namespace(target_namespace target_name ${PROJECT_NAME})
    FetchContent_Declare(${target_name}.${deps_name} ${fetch_cmd})
    set(CMAKE_MESSAGE_LOG_LEVEL NOTICE)
    FetchContent_MakeAvailable(${target_name}.${deps_name})
    unset(CMAKE_MESSAGE_LOG_LEVEL)
    message(STATUS "Add target: ${deps}${opts}")
endfunction()
